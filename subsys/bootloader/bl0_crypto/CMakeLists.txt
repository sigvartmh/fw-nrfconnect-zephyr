
add_library(bl0_crypto STATIC bl0_crypto.c)
if (CONFIG_SB_NRF_OBERON)
  target_link_libraries(bl0_crypto PRIVATE zephyr_interface)

  set(OBERON_BASE ${ZEPHYR_BASE}/ext/lib/crypto/nrf_oberon)
  if (CONFIG_SOC_SERIES_NRF51X)
    set(OBERON_LIB ${OBERON_BASE}/lib/nrf51/liboberon_no_fp_2.0.6.a)
  elseif (CONFIG_SOC_SERIES_NRF52X AND CONFIG_FLOAT)
    set(OBERON_LIB ${OBERON_BASE}/lib/nrf52/liboberon_2.0.6.a)
  elseif (CONFIG_SOC_SERIES_NRF52X)
    set(OBERON_LIB ${OBERON_BASE}/lib/nrf52/liboberon_no_fp_2.0.6.a)
  else()
    message(FATAL_ERROR "nrf_oberon can only be used with Nordic Semiconductor nRF51 and nRF52 series SOCs.")
  endif()

  target_link_libraries(bl0_crypto PRIVATE ${OBERON_LIB})
  target_include_directories(bl0_crypto PRIVATE ${OBERON_BASE}/include)
  target_include_directories(bl0_crypto PUBLIC ../include)

  if (CONFIG_SB_CRYPTO_OBERON_ECDSA_SECP256R1)
    target_sources(bl0_crypto PRIVATE bl0_crypto_oberon_ecdsa.c)
  endif()
  if (CONFIG_SB_CRYPTO_OBERON_SHA256)
    target_sources(bl0_crypto PRIVATE bl0_crypto_oberon_hash.c)
  endif()
else()
  message(FATAL_ERROR "BL0 crypto implementation is missing.")
endif()